.TITLE 'MASTER CONTROLLER BOARD MODIFIED 8080 FIG-FORTH 1.1 VERSION A0'
.SBTTL	'IMPLEMENTED 14 DEC 83 BY C. PHILLIPS (312 386-3147) (312 327-0391)'
;
;	FREE FORTH VERSION WITHOUT DISC SUPPORT
;
;	IMPLEMENTED & RELEASED BY CLYDE PHILLIPS
;	FOR SPACE-TIME PRODUCTIONS 
;	14 DECEMBER 1983
;
;	FOR SALES, SERVICE AND PROPRIETARY PRODUCTS,
;	INCLUDING ENHANCED FORTH BASED SYSTEMS
;	CONTACT:
; 
;	CLYDE PHILLIPS		M.SIMON CHEIF DESIGNER
;	ACCESS EXPRESS		SPACE-TIME PRODUCTIONS
;	509 N MARION		2053 N SHEFFIELD
;	OAK PARK, IL 60302	CHICAGO, IL 60614
;	(312) 386-3147		(312) 327-0391
;				(312) 975-1929
;
;	FOR USE W/ MACRO II ASSEMBLER
;	  AVAILABLE FROM: COMPUTER DESIGN LABS
;			  342 COLUMBUS AVE.
;			  TRENTON, NJ  08629
;			  (800) 458-3491 EXT. 15
;			  (609) 599-2146
;
; NOTE: TO DEBUG ASSEMBLY, LIST FILE MUST BE SCANNED
; 	FOR OBVIOUS MIS-ASSEMBLY ERRORS, SUCH AS A 
;	BLOCK OF CODE ASSEMBLED AS ASCII CHARACTERS...
;
;
;	FIG-FORTH  RELEASE 1.1  FOR THE 8080 PROCESSOR
;
;	ALL PUBLICATIONS OF THE FORTH INTEREST GROUP
;	ARE PUBLIC DOMAIN.  THEY MAY BE FURTHER
;	DISTRIBUTED BY THE INCLUSION OF THIS CREDIT
;	NOTICE:
;
;   THIS PUBLICATION HAS BEEN MADE AVAILABLE BY THE
;		     FORTH INTEREST GROUP
;		     P.O. BOX 1105
;		     SAN CARLOS, CA 94070
;
; 	THIS VERSION 14 DEC 83  10:20z
;
;  RECOMENDED READING
;	'STARTING FORTH' BY LEO BRODIE
;	PRENTICE HALL INC. , 
;		ENGLEWOOD CLIFFS, NJ 07632
;
;	'THREADED INTERPRETIVE LANGUAGES'
;		BY R.G. LOELIGER
;	BYTE BOOKS, 70 MAIN ST
;		PETERBOROUGH, NH 03458
;
;
;	ABORT: HAS SIGN ON MESSAGE
;
;
;	MEMORY ALLOCATION
;RAM IS REFRENCED AT R1: , R2: , R3: , R4:
;EM IS REFRENCED AT BUF1 (AN INTIALISATION CONSTANT)
;	AND EM2: , EM3:
;START IS REFRENCED AT S1: , S2: , S3: , S4: , S5:
;
; FOR MCB2 AT 2000H
;
;EM	==	06800H	;top of memory + 1  
;RAM	==	04000H	; 2 K RAM FOR DICTIONAIRY
;START	==	2100H	;ORG STATEMENT

; FOR MCB2 AT 0000H
;
EM	==	06800H	;top of memory + 1  
RAM	==	02000H	; 2 K RAM FOR DICTIONAIRY
START	==	00100H	;ORG STATEMENT

; FOR MCB AT O000H
;
;EM	==	03800H	;top of memory + 1  
;RAM	==	02000H	; 2 K RAM FOR DICTIONAIRY
;START	==	00100H	;ORG STATEMENT

;	RELEASE AND VERSION NUMBERS
;
FIGREL	==	1	;FIG RELEASE #
FIGREV	==	1	;FIG REVISION #
USRVER	==	61H	;USER VERSION # a
;
;	ASCII CHARACTERS USED
;
ABL	==	20H	;space
ACR	==	0DH	;carraige return
ADOT	==	02EH	;period
BELL	==	07H	;(^G)
BSIN	==	7FH	;input backspace chr = DEL 
BSOUT	==	08H	;output backspace (^h)
LF	==	0AH	;line feed
FF	==	0CH	;formfeed (^L)
DLE	==	10H	; CNTRL P PRINTER TOGGLE
;
;	MEMORY ALLOCATION
;
NSCR	==	1	;# of 1024 Byte Screens
KBBUF	==	128	;data bytes per disk buffer
US	==	40H	;user variables space
RTS	==	0A0H	;Return stack and 
			;   term buff space
;
CO	==	KBBUF+4	;DISK BUFFER + 2 HEADER 
			;   + 2 TAIL

NBUF	==	NSCR*400H/KBBUF	;# OF BUFFERS
BUF1	==	EM-CO*NBUF	;ADDR OF 1st 
				;    DISK BUFFER
INITR0	==	BUF1-US		; (R0)
INITS0	==	INITR0-RTS	; (S0)

;-----------------------------------------------------
; I/O Modified for MASTER CONTROLLER BOARD 
; 21 MARCH 83  M.Simon 

; A SPACE-Time Production

SACON	== 76h			;SIO A CHANNEL 
				;  CONTROL PORT
CTR0	== 6Ch			;8253 CHAN 0
CTR1	== CTR0+1
CTR2	== CTR0+2
CTRTC	== CTR0+3		;8253 CONTROL 
				; CHANNEL

;	<I/O DEVICES>
;
; TELEPRINTER
;
TTI	== 74h		;DATA IN PORT
TTO	== 74h		;DATA OUT PORT
TTS	== 76h		;STATUS PORT (IN)
TTYDA	== 1		;DATA AVAILABLE MASK BIT
TTYBE	== 04		;XMTR BUFFER EMPTY MASK

;--------------------------------------------------
.PABS
.PHEX
.LOC	START-100H
S1:
	DI	
	LXI	H,FORTH-8	; STARTING LINK
R1:	SHLD	RAM		; TO RAM
	LXI	H,0
R2:	SHLD	RAM+2
	LHLD	UPINIT
	SHLD	UP
	SHLD	RPP
S2:	JMP	START

SIOINT:	MVI	B,10.
	MVI	C,SACON
	LXI	H,SIOTAB
	OUTI
	NOP
	OUTI
	NOP
	OUTIR
	NOP
	RET

SIOTAB:
	.BYTE	18h,18h		;RESET
	.BYTE	01h		;CHANNEL 1
	.BYTE	00h		;NO INTERRUPTS
	.BYTE	03h		;CHANNEL 3
	.BYTE	0C1h		;8 DATA, REC 
				;  ENABLE
	.BYTE	04h		;CHANNEL 4
	.BYTE	44h		;X16,1 STOP, NO 
				;     PARITY
	.BYTE	05h		;CHANNEL 5
	.BYTE	0EAh		;DTR,8 DATA,NO 
				; BREAK, XMIT 
				;  ENABLE, RTS

CT53IN:
	MVI	L,0		;INIT CTR 0
	LXI	D,26.		; FOR 2Mhz CLOCK, 
				; 4800 BAUD

; CALLS BELOW ARE TO DUPLICATE CODE OF THE WORD BAUD

	CALL	IRI
	MVI	L,1		; CTR 1
	LXI	D,417.		;  300 BAUD
	CALL	IRI
	MVI	L,2		; CTR 2
	LXI	D,1136.		;  110 BAUD
	CALL	IRI
	RET
IRI:	MOV	A,L		;TEST FOR CTR #
	ORA	A
	JRZ	UTC0
	DCR	A
	JRZ	UTC1
	DCR	A
	JRZ	UTC2
S3:	JNZ	START-100H

UTC0:	MVI	A,00110110B
	OUT	CTRTC
	MOV	A,E
	OUT	CTR0
	MOV	A,D
	OUT	CTR0
	RET

UTC1:	MVI	A,01110110B
	OUT	CTRTC
	MOV	A,E
	OUT	CTR1
	MOV	A,D
	OUT	CTR1
	RET

UTC2:	MVI	A,10110110B
	OUT	CTRTC
	MOV	A,E
	OUT	CTR2
	MOV	A,D
	OUT	CTR2
	RET	

;
.LOC	START
S4:
ORIG:	NOP
	JMP	CLD
	NOP
	JMP	WRM
	.BYTE	FIGREL
	.BYTE	FIGREV
	.BYTE	USRVER
	.BYTE	0EH	;implementation attributes
	.WORD	FORTH-8	;topmost word in FORTH 
			;   vocabulary
	.WORD	BSIN	;backspace char
UPINIT:	.WORD	INITR0	; INIT (UP)

;<<<<<<<Following used by COLD;

;       Must be in same order as user variables
	.WORD	INITS0	; INIT (S0)
	.WORD	INITR0	; INIT (R0)
	.WORD	INITS0	; INIT (TIB)
	.WORD	1FH	; INIT (WIDTH)
	.WORD	0	; INIT (WARNING) 0=NO DISK, 
			;     1=DISK
	.WORD	INITDP	; INIT (FENCE)
	.WORD	INITDP	; INIT (DP)
	.WORD	FORTH+8	; INIT (VOC-LINK)

;<<<<<<<End data used by COLD

;	DEBUG SUPPORT
BIP:	.WORD	0	;breakpoint on IP value
;
TNEXT:	LXI	H,BIP
	MOV	A,M
	CMP	C
	JNZ	TNEXT1
	INX	H
	MOV	A,M
	CMP	B
	JNZ	TNEXT1
BREAK:	NOP		;place breakpoint here
	NOP
	NOP
TNEXT1:	LDAX	B
	INX	B
	MOV	L,A
	JMP	NEXT+3
;
;--------------------
;
;	NEXT, THE FORTH ADDRESS INTERPRETER
;	  ( POST INCREMENTING VERSION )
;
DPUSH:	PUSH	D
HPUSH:	PUSH	H
NEXT:	LDAX	B	;(W) <- ((IP))
	INX	B	;(IP) <- (IP)+2
	MOV	L,A
	LDAX	B	
	INX	B
	MOV	H,A	;(HL) <- CFA
NEXT1:	MOV	E,M	;(PC) <- ((W))
	INX	H
	MOV	D,M
	XCHG
	PCHL		;NOTE: (DE) = CFA+1
;END EQU
;-----------------------
;	FORTH DICTIONARY
DP0:	

;THE "BAUD" PROGRAM GETS TWO PARAMETERS OFF THE 
; STACK. THE FIRST OFF THE STACK IS A TWO BYTE 
; HEX NUMBER THAT IS THE DIVIDER THAT IS TO BE 
; USED BY THE COUNTER. THE SECOND NUMBER IS USED
; TO DETERMINE THE COUNTER TO BE USED.

;DIVIDERS CAN BE FROM 2 TO 0FFFFh, COUNTERS 
;  ARE 0,1, & 2
;  
	.BYTE	84H	; BAUD FOR THE MCB
	.ASCII	'BAU'
	.BYTE	'D'+80H
	.WORD	0	;(LFA)=0 MARKS END OF 
			;  DICTIONARY
BAUD:	.WORD	.+2
			;GET 2 PARAMS
	POP	D		;BAUD RATE
	POP	H		;COUNTER #
BRI:	MOV	A,L		;TEST FOR CTR #
	ORA	A
	JRZ	OUTC0
	DCR	A
	JRZ	OUTC1
	DCR	A
	JRZ	OUTC2
	JNZ	ERROR

OUTC0:	MVI	A,00110110B
	OUT	CTRTC
	MOV	A,E
	OUT	CTR0
	MOV	A,D
	OUT	CTR0
	JMP	NEXT

OUTC1:	MVI	A,01110110B
	OUT	CTRTC
	MOV	A,E
	OUT	CTR1
	MOV	A,D
	OUT	CTR1
	JMP	NEXT

OUTC2:	MVI	A,10110110B
	OUT	CTRTC
	MOV	A,E
	OUT	CTR2
	MOV	A,D
	OUT	CTR2
	JMP	NEXT

	.BYTE	83H	;LIT
	.ASCII	'LI'
	.BYTE	'T'+80H
	.WORD	BAUD-7	; LINK TO LAST ENTRY
LIT:	.WORD	.+2
	LDAX	B
	INX	B
	MOV	L,A
	LDAX	B
	INX	B
	MOV	H,A
	JMP	HPUSH
;
	.BYTE	87H	;EXECUTE
	.ASCII	'EXECUT'
	.BYTE	'E'+80H
	.WORD	LIT-6
EXEC:	.WORD	.+2
	POP	H
	JMP	NEXT1
;
	.BYTE	86H	;BRANCH
	.ASCII	'BRANC'
	.BYTE	'H'+80H
	.WORD	EXEC-0AH
BRAN:	.WORD	.+2
BRAN1:	MOV	H,B
	MOV	L,C
	MOV	E,M
	INX	H
	MOV	D,M
	DCX	H
	DAD	D
	MOV	C,L
	MOV	B,H
	JMP	NEXT
;
	.BYTE	87H	;0BRANCH
	.ASCII	'0BRANC'
	.BYTE	'H'+80H
	.WORD	BRAN-9
ZBRAN:	.WORD	.+2
	POP	H
	MOV	A,L
	ORA	H
	JZ	BRAN1
	INX	B
	INX	B
	JMP	NEXT
;
	.BYTE	86H	;(LOOP)
	.ASCII	'(LOOP'
	.BYTE	')'+80H
	.WORD	ZBRAN-0AH
XLOOP:	.WORD	.+2
	LXI	D,1
XLOO1:	LHLD	RPP
	MOV	A,M
	ADD	E
	MOV	M,A
	MOV	E,A
	INX	H
	MOV	A,M
	ADC	D
	MOV	M,A
	INX	H
	INR	D
	DCR	D
	MOV	D,A
	JM	XLOO2
	MOV	A,E
	SUB	M
	MOV	A,D
	INX	H
	SBB	M
	JMP	XLOO3
XLOO2:	MOV	A,M
	SUB	E
	INX	H
	MOV	A,M
	SBB	D
;
XLOO3:	JM	BRAN1
	INX	H
	SHLD	RPP
	INX	B
	INX	B
	JMP	NEXT
;
	.BYTE	87H	; (+LOOP)
	.ASCII	'(+LOOP'
	.BYTE	')'+80H
	.WORD	XLOOP-9
XPLOO:	.WORD	.+2
	POP	D
	JMP	XLOO1
;
	.BYTE	84H	;(DO)
	.ASCII	'(DO'
	.BYTE	')'+80H
	.WORD	XPLOO-0AH
XDO:	.WORD	.+2
	LHLD	RPP
	DCX	H
	DCX	H
	DCX	H
	DCX	H
	SHLD	RPP
	POP	D
	MOV	M,E
	INX	H
	MOV	M,D
	POP	D
	INX	H
	MOV	M,E
	INX	H
	MOV	M,D
	JMP	NEXT
;
	.BYTE	81H	; I
	.BYTE	'I'+80H
	.WORD	XDO-7
IDO:	.WORD	.+2
	LHLD	RPP
	MOV	E,M
	INX	H
	MOV	D,M
	PUSH	D
	JMP	NEXT
;
	.BYTE	85H	;DIGIT
	.ASCII	'DIGI'
	.BYTE	'T'+80H
	.WORD	IDO-4
DIGIT:	.WORD	.+2
	POP	H
;
	POP	D
	MOV	A,E
	SUI	30H
	JM	DIGI2
	CPI	0AH
	JM	DIGI1
	SUI	7
	CPI	0AH
	JM	DIGI2
;
DIGI1:	CMP	L
	JP	DIGI2
;
	MOV	E,A
	LXI	H,1
	JMP	DPUSH
;
DIGI2:	MOV	L,H
	JMP	HPUSH
;
	.BYTE	86H	;(FIND) (2-1)FAILURE
	.ASCII	'(FIND'	;       (2-3)SUCCESS
	.BYTE	')'+80H
	.WORD	DIGIT-8
PFIND:	.WORD	.+2
	POP	D
PFIN1:	POP	H
	PUSH	H
	LDAX	D
	XRA	M
	ANI	3FH
	JNZ	PFIN4
;
PFIN2:	INX	H
	INX	D
	LDAX	D
	XRA	M
	ADD	A
	JNZ	PFIN3
	JNC	PFIN2
	LXI	H,5
	DAD	D
	XTHL
;
PFIN6:	DCX	D
	LDAX	D
	ORA	A
	JP	PFIN6
	MOV	E,A
	MVI	D,0
	LXI	H,1
	JMP	DPUSH
;
PFIN3:	JC	PFIN5
PFIN4:	INX	D
	LDAX	D
	ORA	A
	JP	PFIN4
PFIN5:	INX	D
	XCHG
	MOV	E,M
	INX	H
	MOV	D,M
	MOV	A,D
	ORA	E
	JNZ	PFIN1
;
	POP	H
	LXI	H,0
	JMP	HPUSH
;
	.BYTE	87H	;ENCLOSE
	.ASCII	'ENCLOS'
	.BYTE	'E'+80H
	.WORD	PFIND-9
ENCL:	.WORD	.+2
	POP	D
	POP	H
	PUSH	H
	MOV	A,E
	MOV	D,A
	MVI	E,-1
	DCX	H
;
ENCL1:	INX	H
	INR	E
	CMP	M
	JZ	ENCL1
;
	MVI	D,0
	PUSH	D
	MOV	D,A
	MOV	A,M
	ANA	A
	JNZ	ENCL2
	MVI	D,0
	INR	E
	PUSH	D
	DCR	E
	PUSH	D
	JMP	NEXT
;
;
ENCL2:	MOV	A,D
	INX	H
	INR	E
	CMP	M
	JZ	ENCL4
	MOV	A,M
	ANA	A
	JNZ	ENCL2
;
ENCL3:	MVI	D,0
	PUSH	D
	PUSH	D
	JMP	NEXT
;
ENCL4:	MVI	D,0
;
	PUSH	D
	INR	E
;
	PUSH	D
	JMP	NEXT
;END D1
; EMIT FILE
	.BYTE	84H	;EMIT
	.ASCII	'EMI'
	.BYTE	'T'+80H
	.WORD	ENCL-0AH
EMIT:	.WORD	DOCOL
	.WORD	PEMIT
	.WORD	ONE
	.WORD	OUTT
	.WORD	PSTOR
	.WORD	SEMIS
;
	.BYTE	83H	;KEY
	.ASCII	'KE'
	.BYTE	'Y'+80H
	.WORD	EMIT-7
KEY:	.WORD	.+2
	JMP	PKEY
;
	.BYTE	89H	;?TERMINAL
	.ASCII	'?TERMINA'
	.BYTE	'L'+80H
	.WORD	KEY-6
QTERM:	.WORD	.+2
	LXI	H,0
	JMP	PQTER
;
	.BYTE	82H	;CR
	.BYTE	'C'
	.BYTE	'R'+80H
	.WORD	QTERM-0CH
CR:	.WORD	.+2
	JMP	PCR
;END EMIT
; D2 FILE
	.BYTE	85H	;CMOVE
	.ASCII	'CMOV'
	.BYTE	'E'+80H
	.WORD	CR-5
CMOVE:	.WORD	.+2
	MOV	L,C
	MOV	H,B
	POP	B
	POP	D
	XTHL
;
	JMP	CMOV2
CMOV1:	MOV	A,M
	INX	H
	STAX	D
	INX	D
	DCX	B
CMOV2:	MOV	A,B
	ORA	C
	JNZ	CMOV1
	POP	B
	JMP	NEXT
;
	.BYTE	82H	; U*   16x16 UNSIGNED MULTIPLY
	.BYTE	'U'	;average execution 
			;  time = 994 cycles (8080)
	.BYTE	'*'+80H
	.WORD	CMOVE-8
USTAR:	.WORD	.+2
	POP	D
	POP	H
	PUSH	B
	MOV	B,H
	MOV	A,L
	CALL	MPYX
;
	PUSH	H
	MOV	H,A
	MOV	A,B
	MOV	B,H
	CALL	MPYX
;
	POP	D
	MOV	C,D
;	FORM SUM OF PARTIALS:
;
;
;
;
	DAD	B
	ACI	0
	MOV	D,L
	MOV	L,H
	MOV	H,A
	POP	B
	PUSH	D
	JMP	HPUSH
;
MPYX:	LXI	H,0
	MVI	C,8
MPYX1:	DAD	H
	RAL
	JNC	MPYX2
	DAD	D
	ACI	0
MPYX2:	DCR	C
	JNZ	MPYX1
	RET
;
	.BYTE	82H	; U/
	.BYTE	'U'
	.BYTE	'/'+80H
	.WORD	USTAR-5
USLAS:	.WORD	.+2
	LXI	H,4
	DAD	SP
	MOV	E,M
	MOV	M,C
	INX	H
	MOV	D,M
	MOV	M,B
	POP	B
	POP	H
	MOV	A,L
	SUB	C
	MOV	A,H
	SBB	B
	JC	USLA1
	LXI	H,0FFFFH
	LXI	D,0FFFFH
	JMP	USLA7
USLA1:	MVI	A,16
USLA2:	DAD	H
	RAL
	XCHG
	DAD	H
	JNC	USLA3
	INX	D
	ANA	A
USLA3:	XCHG
	RAR
	PUSH	PSW
	JNC	USLA4
	MOV	A,L
	SUB	C
	MOV	L,A
	MOV	A,H
	SBB	B
	MOV	H,A
	JMP	USLA5
USLA4:	MOV	A,L
	SUB	C
	MOV	L,A
	MOV	A,H
	SBB	B
	MOV	H,A
	JNC	USLA5
	DAD	B
	DCX	D
USLA5:	INX	D
USLA6:	POP	PSW
	DCR	A
	JNZ	USLA2
USLA7:	POP	B
	PUSH	H
	PUSH	D
	JMP	NEXT
;
	.BYTE	83H	; AND
	.ASCII	'AN'
	.BYTE	'D'+80H
	.WORD	USLAS-5
ANDD:	.WORD	.+2
	POP	D
	POP	H
	MOV	A,E
	ANA	L
	MOV	L,A
	MOV	A,D
	ANA	H
	MOV	H,A
	JMP	HPUSH
;
	.BYTE	82H	; OR
	.BYTE	'O'
	.BYTE	'R'+80H
	.WORD	ANDD-6
ORR:	.WORD	.+2
	POP	D
	POP	H
	MOV	A,E
	ORA	L
	MOV	L,A
	MOV	A,D
	ORA	H
	MOV	H,A
	JMP	HPUSH
;
	.BYTE	83H	; XOR
	.ASCII	'XO'
	.BYTE	'R'+80H
	.WORD	ORR-5
XORR:	.WORD	.+2
	POP	D
	POP	H
	MOV	A,E
	XRA	L
	MOV	L,A
	MOV	A,D
	XRA	H
	MOV	H,A
	JMP	HPUSH
;
	.BYTE	83H	; SP@
	.ASCII	'SP'
	.BYTE	'@'+80H
	.WORD	XORR-6
SPAT:	.WORD	.+2
	LXI	H,0
	DAD	SP
	JMP	HPUSH
;
	.BYTE	83H	;STACK POINTER STORE
	.ASCII	'SP'
	.BYTE	'!'+80H
	.WORD	SPAT-6
SPSTO:	.WORD	.+2
	LHLD	UP
	LXI	D,6
	DAD	D
	MOV	E,M
	INX	H
	MOV	D,M
	XCHG
	SPHL
	JMP	NEXT
;
	.BYTE	83H	; RP@
	.ASCII	'RP'
	.BYTE	'@'+80H
	.WORD	SPSTO-6
RPAT:	.WORD	.+2
	LHLD	RPP
	JMP	HPUSH
;
	.BYTE	83H	; RETURN STACK POINTER STORE
	.ASCII	'RP'
	.BYTE	'!'+80H
	.WORD	RPAT-6
RPSTO:	.WORD	.+2
	LHLD	UP
	LXI	D,8
	DAD	D
	MOV	E,M
	INX	H
	MOV	D,M
	XCHG
	SHLD	RPP
	JMP	NEXT
;
	.BYTE	82H	;  ;S
	.BYTE	';'
	.BYTE	'S'+80H
	.WORD	RPSTO-6
SEMIS:	.WORD	.+2
	LHLD	RPP
	MOV	C,M
	INX	H
	MOV	B,M
	INX	H
	SHLD	RPP
	JMP	NEXT
;
	.BYTE	85H	; LEAVE
	.ASCII	'LEAV'
	.BYTE	'E'+80H
	.WORD	SEMIS-5
LEAVE:	.WORD	.+2
	LHLD	RPP
	MOV	E,M
	INX	H
	MOV	D,M
	INX	H
	MOV	M,E
	INX	H
	MOV	M,D
	JMP	NEXT
;
	.BYTE	82H	; >R
	.BYTE	'>'
	.BYTE	'R'+80H
	.WORD	LEAVE-8
TOR:	.WORD	.+2
	POP	D
	LHLD	RPP
	DCX	H
	DCX	H
	SHLD	RPP
	MOV	M,E
	INX	H
	MOV	M,D
	JMP	NEXT
;
	.BYTE	82H	; R>
	.BYTE	'R'
	.BYTE	'>'+80H
	.WORD	TOR-5
FROMR:	.WORD	.+2
	LHLD	RPP
	MOV	E,M
	INX	H
	MOV	D,M
	INX	H
	SHLD	RPP
	PUSH	D
	JMP	NEXT
;
	.BYTE	81H	; R
	.BYTE	'R'+80H
	.WORD	FROMR-5
RR:	.WORD	IDO+2
;
	.BYTE	82H	; 0=
	.BYTE	'0'
	.BYTE	'='+80H
	.WORD	RR-4
ZEQU:	.WORD	.+2
	POP	H
	MOV	A,L
	ORA	H
	LXI	H,0
	JNZ	ZEQU1
	INX	H
ZEQU1:	JMP	HPUSH
;
	.BYTE	82H	; 0<
	.BYTE	'0'
	.BYTE	'<'+80H
	.WORD	ZEQU-5
ZLESS:	.WORD	.+2
	POP	H
	DAD	H
	LXI	H,0
	JNC	ZLES1
	INX	H
ZLES1:	JMP	HPUSH
;
	.BYTE	81H	; +
	.BYTE	'+'+80H
	.WORD	ZLESS-5
PLUS:	.WORD	.+2
	POP	D
	POP	H
	DAD	D
	JMP	HPUSH
;
	.BYTE	82H	; D+  ( 4-2 )
	.BYTE	'D'
	.BYTE	'+'+80H
	.WORD	PLUS-4
DPLUS:	.WORD	.+2
	LXI	H,6
	DAD	SP
	MOV	E,M
	MOV	M,C
	INX	H
	MOV	D,M
	MOV	M,B
	POP	B
	POP	H
	DAD	D
	XCHG
	POP	H
	MOV	A,L
	ADC	C
	MOV	L,A
	MOV	A,H
	ADC	B
	MOV	H,A
	POP	B
	PUSH	D
	JMP	HPUSH
;
	.BYTE	85H	; MINUS
	.ASCII	'MINU'
	.BYTE	'S'+80H
	.WORD	DPLUS-5
MINUS:	.WORD	.+2
	POP	H
	MOV	A,L
	CMA
	MOV	L,A
	MOV	A,H
	CMA
	MOV	H,A
	INX	H
	JMP	HPUSH
;
	.BYTE	86H	; DMINUS
	.ASCII	'DMINU'
	.BYTE	'S'+80H
	.WORD	MINUS-8
DMINU:	.WORD	.+2
	POP	H
	POP	D
	SUB	A
	SUB	E
	MOV	E,A
	MVI	A,0
	SBB	D
	MOV	D,A
	MVI	A,0
	SBB	L
	MOV	L,A
	MVI	A,0
	SBB	H
	MOV	H,A
	PUSH	D
	JMP	HPUSH
;
	.BYTE	84H	; OVER
	.ASCII	'OVE'
	.BYTE	'R'+80H
	.WORD	DMINU-9
OVER:	.WORD	.+2
	POP	D
	POP	H
	PUSH	H
	JMP	DPUSH
;
	.BYTE	84H	; DROP
	.ASCII	'DRO'
	.BYTE	'P'+80H
	.WORD	OVER-7
DROP:	.WORD	.+2
	POP	H
	JMP	NEXT
;
	.BYTE	84H	; SWAP
	.ASCII	'SWA'
	.BYTE	'P'+80H
	.WORD	DROP-7
SWAP:	.WORD	.+2
	POP	H
	XTHL
	JMP	HPUSH
;
	.BYTE	83H	; DUP
	.ASCII	'DU'
	.BYTE	'P'+80H
	.WORD	SWAP-7
DUP:	.WORD	.+2
	POP	H
	PUSH	H
	JMP	HPUSH
;
	.BYTE	84H	; 2DUP
	.ASCII	'2DU'
	.BYTE	'P'+80H
	.WORD	DUP-6
TDUP:	.WORD	.+2
	POP	H
	POP	D
	PUSH	D
	PUSH	H
	JMP	DPUSH
;
	.BYTE	82H	; PLUS STORE
	.BYTE	'+'
	.BYTE	'!'+80H
	.WORD	TDUP-7
PSTOR:	.WORD	.+2
	POP	H
	POP	D
	MOV	A,M
	ADD	E
	MOV	M,A
	INX	H
	MOV	A,M
	ADC	D
	MOV	M,A
	JMP	NEXT
;
	.BYTE	86H	; TOGGLE
	.ASCII	'TOGGL'
	.BYTE	'E'+80H
	.WORD	PSTOR-5
TOGGL:	.WORD	.+2
	POP	D
	POP	H
	MOV	A,M
	XRA	E
	MOV	M,A
	JMP	NEXT
;
	.BYTE	81H	; @
	.BYTE	'@'+80H
	.WORD	TOGGL-9
AT:	.WORD	.+2
	POP	H
	MOV	E,M
	INX	H
	MOV	D,M
	PUSH	D
	JMP	NEXT
;
	.BYTE	82H	; C@
	.BYTE	'C'
	.BYTE	'@'+80H
	.WORD	AT-4
CAT:	.WORD	.+2
	POP	H
	MOV	L,M
	MVI	H,0
	JMP	HPUSH
;
	.BYTE	82H	; 2@
	.BYTE	'2'
	.BYTE	'@'+80H
	.WORD	CAT-5
TAT:	.WORD	.+2
	POP	H
	LXI	D,2
	DAD	D
	MOV	E,M
	INX	H
	MOV	D,M
	PUSH	D
	LXI	D,-3
	DAD	D
	MOV	E,M
	INX	H
	MOV	D,M
	PUSH	D
	JMP	NEXT
;
	.BYTE	81H	; STORE
	.BYTE	'!'+80H
	.WORD	TAT-5
STORE:	.WORD	.+2
	POP	H
	POP	D
	MOV	M,E
	INX	H
	MOV	M,D
	JMP	NEXT
;
	.BYTE	82H	; C STORE
	.BYTE	'C'
	.BYTE	'!'+80H
	.WORD	STORE-4
CSTOR:	.WORD	.+2
	POP	H
	POP	D
	MOV	M,E
	JMP	NEXT
;
	.BYTE	82H	; 2 STORE
	.BYTE	'2'
	.BYTE	'!'+80H
	.WORD	CSTOR-5
TSTOR:	.WORD	.+2
	POP	H
	POP	D
	MOV	M,E
	INX	H
	MOV	M,D
	INX	H
	POP	D
	MOV	M,E
	INX	H
	MOV	M,D
	JMP	NEXT
;
	.BYTE	0C1H	;  :  (COLON)
	.BYTE	':'+80H
	.WORD	TSTOR-5
COLON:	.WORD	DOCOL
	.WORD	QEXEC
	.WORD	SCSP
	.WORD	CURR
	.WORD	AT
	.WORD	CONT
	.WORD	STORE
	.WORD	CREAT
	.WORD	RBRAC
	.WORD	PSCOD
DOCOL:	LHLD	RPP
	DCX	H
	MOV	M,B
	DCX	H
	MOV	M,C
	SHLD	RPP
	INX	D
	MOV	C,E
	MOV	B,D
	JMP	NEXT
;
	.BYTE	0C1H	;  ;  (SEMICOLON)
	.BYTE	';'+80H
	.WORD	COLON-4
SEMI:	.WORD	DOCOL
	.WORD	QCSP
	.WORD	COMP
	.WORD	SEMIS
	.WORD	SMUDG
	.WORD	LBRAC
	.WORD	SEMIS
;
	.BYTE	84H	; NOOP
	.ASCII	'NOO'
	.BYTE	'P'+80H
	.WORD	SEMI-4
NOOP:	.WORD	DOCOL
	.WORD	SEMIS
;
	.BYTE	88H	; CONSTANT
	.ASCII	'CONSTAN'
	.BYTE	'T'+80H
	.WORD	NOOP-7
CON:	.WORD	DOCOL
	.WORD	CREAT
	.WORD	SMUDG
	.WORD	COMMA
	.WORD	PSCOD
DOCON:	INX	D
	XCHG
	MOV	E,M
	INX	H
	MOV	D,M
	PUSH	D
	JMP	NEXT
;
	.BYTE	88H	; VARIABLE
	.ASCII	'VARIABL'
	.BYTE	'E'+80H
	.WORD	CON-0BH
VAR:	.WORD	DOCOL
	.WORD	CON
	.WORD	PSCOD
DOVAR:	INX	D
	PUSH	D
	JMP	NEXT
;
	.BYTE	84H	; USER
	.ASCII	'USE'
	.BYTE	'R'+80H
	.WORD	VAR-0BH
USER:	.WORD	DOCOL
	.WORD	CON
	.WORD	PSCOD
DOUSE:	INX	D
	XCHG
	MOV	E,M
	MVI	D,0
	LHLD	UP
	DAD	D
	JMP	HPUSH
;
	.BYTE	81H	; 0
	.BYTE	'0'+80H
	.WORD	USER-7
ZERO:	.WORD	DOCON
	.WORD	0
;
	.BYTE	81H	; 1
	.BYTE	'1'+80H
	.WORD	ZERO-4
ONE:	.WORD	DOCON
	.WORD	1
;
	.BYTE	81H	; 2
	.BYTE	'2'+80H
	.WORD	ONE-4
TWO:	.WORD	DOCON
	.WORD	2
;
	.BYTE	81H	; 3
	.BYTE	'3'+80H
	.WORD	TWO-4
THREE:	.WORD	DOCON
	.WORD	3
;
	.BYTE	82H	; BL
	.BYTE	'B'
	.BYTE	'L'+80H
	.WORD	THREE-4
BL:	.WORD	DOCON
	.WORD	20H
;
	.BYTE	83H	; C/L  (CHARACTERS/LINE)
	.ASCII	'C/'
	.BYTE	'L'+80H
	.WORD	BL-5
CSLL:	.WORD	DOCON
	.WORD	64
;
	.BYTE	85H	; FIRST
	.ASCII	'FIRS'
	.BYTE	'T'+80H
	.WORD	CSLL-6
FIRST:	.WORD	DOCON
	.WORD	BUF1
;
	.BYTE	85H	; LIMIT
	.ASCII	'LIMI'
	.BYTE	'T'+80H
	.WORD	FIRST-8
LIMIT:	.WORD	DOCON
EM2:	.WORD	EM
;
	.BYTE	85H	; B/BUF ( BYTES/BUFFER )
	.ASCII	'B/BU'
	.BYTE	'F'+80H
	.WORD	LIMIT-8
BBUF:	.WORD	DOCON
	.WORD	KBBUF
;
	.BYTE	85H	; B/SCR ( BUFFERS/SCREEN )
	.ASCII	'B/SC'
	.BYTE	'R'+80H
	.WORD	BBUF-8
BSCR:	.WORD	DOCON
	.WORD	400H/KBBUF
;
	.BYTE	87H	; +ORGIN
	.ASCII	'+ORIGI'
	.BYTE	'N'+80H
	.WORD	BSCR-8
PORIG:	.WORD	DOCOL
	.WORD	LIT
	.WORD	ORIG
	.WORD	PLUS
	.WORD	SEMIS
; END D2
; USER FILE
;	USER VARIABLES
;
	.BYTE	82H	; S0
	.BYTE	'S'
	.BYTE	'0'+80H
	.WORD	PORIG-0AH
SZERO:	.WORD	DOUSE
	.WORD	6
;
	.BYTE	82H	; RO

	.BYTÅ	'R'
	.BYTÅ	'0'+80H
	.WORÄ	SZERO-5
RZEROº	.WORÄ	DOUSE
	.WORÄ	8
;
	.BYTÅ	83È	» TIB
	.ASCII	'TI'
	.BYTÅ	'B'+80H
	.WORÄ	RZERO-5
TIBº	.WORÄ	DOUSE
	.BYTÅ	0AH
;
	.BYTÅ	85È	» WIDTH
	.ASCII	'WIDT'
	.BYTÅ	'H'+80H
	.WORÄ	TIB-6
WIDTHº	.WORÄ	DOUSE
	.BYTÅ	0CH
;
	.BYTÅ	87È	» WARNING
	.ASCII	'WARNIN'
	.BYTÅ	'G'+80H
	.WORÄ	WIDTH-8
WARNº	.WORÄ	DOUSE
	.BYTÅ	0EH
;
	.BYTÅ	85È	» FENCE
	.ASCII	'FENC'
	.BYTÅ	'E'+80H
	.WORÄ	WARN-0AH
FENCEº	.WORÄ	DOUSE
	.BYTÅ	10H
;
	.BYTÅ	82È	» DP
	.BYTÅ	'D'
	.BYTÅ	'P'+80H
	.WORÄ	FENCE-8
DPº	.WORÄ	DOUSE
	.BYTÅ	12H
;
	.BYTÅ	88È	» VOC-LINK
	.ASCII	'VOC-LIN'
	.BYTÅ	'K'+80H
	.WORÄ	DP-5
VOCLº	.WORÄ	DOUSE
	.WORÄ	14H
;
	.BYTÅ	83È	» BLK
	.ASCII	'BL'
	.BYTÅ	'K'+80H
	.WORÄ	VOCL-0BH
BLKº	.WORÄ	DOUSE
	.BYTÅ	16H
;
	.BYTÅ	82È	» IN
	.BYTÅ	'I'
	.BYTÅ	'N'+80H
	.WORÄ	BLK-6
INNº	.WORÄ	DOUSE
	.BYTÅ	18H
;
	.BYTÅ	83È	» OUT
	.ASCII	'OU'
	.BYTÅ	'T'+80H
	.WORÄ	INN-5
OUTTº	.WORÄ	DOUSE
	.BYTÅ	1AH
;
	.BYTÅ	83È	;SCR
	.ASCII	'SC'
	.BYTÅ	'R'+80H
	.WORÄ	OUTT-6
SCRº	.WORÄ	DOUSE
	.BYTÅ	1CH
;
	.BYTÅ	86È	» OFFSET
	.ASCII	'OFFSE'
	.BYTÅ	'T'+80H
	.WORÄ	SCR-6
OFSETº	.WORÄ	DOUSE
	.BYTÅ	1EH
;
	.BYTÅ	87È	» CONTEXT
	.ASCII	'CONTEX'
	.BYTÅ	'T'+80H
	.WORÄ	OFSET-9
CONTº	.WORÄ	DOUSE
	.BYTÅ	20H
;
	.BYTÅ	87È	» CURRENT
	.ASCII	'CURREN'
	.BYTÅ	'T'+80H
	.WORÄ	CONT-0AH
CURRº	.WORÄ	DOUSE
	.BYTÅ	22H
;
	.BYTÅ	85È	» STATE
	.ASCII	'STAT'
	.BYTÅ	'E'+80H
	.WORÄ	CURR-0AH
STATEº	.WORÄ	DOUSE
	.BYTÅ	24H
;
	.BYTÅ	84È	» BASE
	.ASCII	'BAS'
	.BYTÅ	'E'+80H
	.WORÄ	STATE-8
BASEº	.WORÄ	DOUSE
	.BYTÅ	26H
;
	.BYTÅ	83È	» DPL
	.ASCII	'DP'
	.BYTÅ	'L'+80H
	.WORÄ	BASE-7
DPLº	.WORÄ	DOUSE
	.BYTÅ	28H
;
	.BYTÅ	83È	» FLD
	.ASCII	'FL'
	.BYTÅ	'D'+80H
	.WORÄ	DPL-6
FLDº	.WORÄ	DOUSE
	.BYTÅ	2AH
;
	.BYTÅ	83È	» CSP
	.ASCII	'CS'
	.BYTÅ	'P'+80H
	.WORÄ	FLD-6
CSPPº	.WORÄ	DOUSE
	.BYTÅ	2CH
;
	.BYTÅ	82È	» R#
	.BYTÅ	'R'
	.BYTÅ	'#'+80H
	.WORÄ	CSPP-6
RNUMº	.WORÄ	DOUSE
	.BYTÅ	2EH
;
	.BYTÅ	83È	» HLD
	.ASCII	'HL'
	.BYTÅ	'D'+80H
	.WORÄ	RNUM-5
HLDº	.WORÄ	DOUSE
	.WORÄ	30H
;
»	ENÄ OÆ USEÒ VARIABLES
;
	.BYTE	82H	; 1+
	.BYTE	'1'
	.BYTE	'+'+80H
	.WORD	HLD-6
ONEP:	.WORD	DOCOL
	.WORD	ONE
	.WORD	PLUS
	.WORD	SEMIS
;
	.BYTE	82H	; 2+
	.BYTE	'2'
	.BYTE	'+'+80H
	.WORD	ONEP-5
TWOP:	.WORD	DOCOL
	.WORD	TWO
	.WORD	PLUS
	.WORD	SEMIS
;
	.BYTE	84H	; HERE
	.ASCII	'HER'
	.BYTE	'E'+80H
	.WORD	TWOP-5
HERE:	.WORD	DOCOL
	.WORD	DP
	.WORD	AT
	.WORD	SEMIS
;
	.BYTE	85H	; ALLOT
	.ASCII	'ALLO'
	.BYTE	'T'+80H
	.WORD	HERE-7
ALLOT:	.WORD	DOCOL
	.WORD	DP
	.WORD	PSTOR
	.WORD	SEMIS
;
	.BYTE	81H	; , (COMMA)
	.BYTE	','+80H
	.WORD	ALLOT-8
COMMA:	.WORD	DOCOL
	.WORD	HERE
	.WORD	STORE
	.WORD	TWO
	.WORD	ALLOT
	.WORD	SEMIS
;
	.BYTE	82H	; C,
	.BYTE	'C'
	.BYTE	','+80H
	.WORD	COMMA-4
CCOMML:	.WORD	DOCOL
	.WORD	HERE
	.WORD	CSTOR
	.WORD	ONE
	.WORD	ALLOT
	.WORD	SEMIS
;
;	SUBROUTINE USED BY - AND <
;
SSUB:	MOV	A,L
	SUB	E
	MOV	L,A
	MOV	A,H
	SBB	D
	MOV	H,A
	RET
;
	.BYTE	81H	; -  (MINUS SIGN)
	.BYTE	'-'+80H
	.WORD	CCOMML-5
SUBB:	.WORD	.+2
	POP	D
	POP	H
	CALL	SSUB
	JMP	HPUSH
;
	.BYTE	81H	; =   (EQUAL SIGN)
	.BYTE	'='+80H
	.WORD	SUBB-4
EQUAL:	.WORD	DOCOL
	.WORD	SUBB
	.WORD	ZEQU
	.WORD	SEMIS
;
	.BYTE	81H	; < (LESS THAN)
	.BYTE	'<'+80H
	.WORD	EQUAL-4
LESS:	.WORD	.+2
	POP	D
	POP	H
	MOV	A,D
	XRA	H
	JM	LES1
	CALL	SSUB
LES1:	INR	H
	DCR	H
	JM	LES2
	LXI	H,0
	JMP	HPUSH
LES2:	LXI	H,1
	JMP	HPUSH
;
	.BYTE	82H	; U<  (UNSIGNED < )
	.BYTE	'U'
	.BYTE	'<'+80H
	.WORD	LESS-4
ULESS:	.WORD	DOCOL
	.WORD	TDUP
	.WORD	XORR
	.WORD	ZLESS
	.WORD	ZBRAN
	.WORD	ULES1-.	; IF
	.WORD	DROP
	.WORD	ZLESS
	.WORD	ZEQU
	.WORD	BRAN
	.WORD	ULES2-.
ULES1:	.WORD	SUBB
	.WORD	ZLESS	; ELSE
ULES2:	.WORD	SEMIS	; ENDIF
;
	.BYTE	81H	; >   (GREATER THAN)
	.BYTE	'>'+80H
	.WORD	ULESS-5
GREAT:	.WORD	DOCOL
	.WORD	SWAP
	.WORD	LESS
	.WORD	SEMIS
;
	.BYTE	83H	; ROT
	.ASCII	'RO'
	.BYTE	'T'+80H
	.WORD	GREAT-4
ROT:	.WORD	.+2
	POP	D
	POP	H
	XTHL
	JMP	DPUSH
;
	.BYTE	85H	; SPACE
	.ASCII	'SPAC'
	.BYTE	'E'+80H
	.WORD	ROT-6
SPACE:	.WORD	DOCOL
	.WORD	BL
	.WORD	EMIT
	.WORD	SEMIS
;
	.BYTE	84H	; -DUP
	.ASCII	'-DU'
	.BYTE	'P'+80H
	.WORD	SPACE-8
DDUP:	.WORD	DOCOL
	.WORD	DUP
	.WORD	ZBRAN	 ; IF
	.WORD	DDUP1-.
	.WORD	DUP	; ENDIF
DDUP1:	.WORD	SEMIS
;
	.BYTE	88H	; TRAVERSE
	.ASCII	'TRAVERS'
	.BYTE	'E'+80H
	.WORD	DDUP-7
TRAV:	.WORD	DOCOL
	.WORD	SWAP
TRAV1:	.WORD	OVER	; BEGIN
	.WORD	PLUS
	.WORD	LIT
	.WORD	7FH	
	.WORD	OVER
	.WORD	CAT
	.WORD	LESS
	.WORD	ZBRAN	; UNTIL
	.WORD	TRAV1-.
	.WORD	SWAP
	.WORD	DROP
	.WORD	SEMIS
;
	.BYTE	86H	; LATEST
	.ASCII	'LATES'
	.BYTE	'T'+80H
	.WORD	TRAV-0BH
LATES:	.WORD	DOCOL
	.WORD	CURR
	.WORD	AT
	.WORD	AT
	.WORD	SEMIS
;
	.BYTE	83H	; LFA
	.ASCII	'LF'
	.BYTE	'A'+80H
	.WORD	LATES-9
LFA:	.WORD	DOCOL
	.WORD	LIT
	.WORD	4
	.WORD	SUBB
	.WORD	SEMIS
;
	.BYTE	83H	; CFA
	.ASCII	'CF'
	.BYTE	'A'+80H
	.WORD	LFA-6
CFA:	.WORD	DOCOL
	.WORD	TWO
	.WORD	SUBB
	.WORD	SEMIS
;
	.BYTE	83H	; NFA
	.ASCII	'NF'
	.BYTE	'A'+80H
	.WORD	CFA-6
NFA:	.WORD	DOCOL
	.WORD	LIT
	.WORD	5
	.WORD	SUBB
	.WORD	LIT
	.WORD	-1
	.WORD	TRAV
	.WORD	SEMIS
;
	.BYTE	83H	; PFA
	.ASCII	'PF'
	.BYTE	'A'+80H
	.WORD	NFA-6
PFA:	.WORD	DOCOL
	.WORD	ONE
	.WORD	TRAV
	.WORD	LIT
	.WORD	5
	.WORD	PLUS
	.WORD	SEMIS
;
	.BYTE	84H	; STORE CSP
	.ASCII	'!CS'
	.BYTE	'P'+80H
	.WORD	PFA-6
SCSP:	.WORD	DOCOL
	.WORD	SPAT
	.WORD	CSPP
	.WORD	STORE
	.WORD	SEMIS
;
	.BYTE	86H	; ?ERROR
	.ASCII	'?ERRO'
	.BYTE	'R'+80H
	.WORD	SCSP-7
QERR:	.WORD	DOCOL
	.WORD	SWAP
	.WORD	ZBRAN	; IF
	.WORD	QERR1-.
	.WORD	ERROR
	.WORD	BRAN	; ELSE
	.WORD	QERR1-.
QERR1:	.WORD	DROP	; ENDIF
QERR2:	.WORD	SEMIS
;
	.BYTE	85H	; ?COMP
	.ASCII	'?COM'
	.BYTE	'P'+80H
	.WORD	QERR-9
QCOMP:	.WORD	DOCOL
	.WORD	STATE
	.WORD	AT
	.WORD	ZEQU
	.WORD	LIT
	.WORD	11H
	.WORD	QERR
	.WORD	SEMIS
;
	.BYTE	85H	; ?EXEC
	.ASCII	'?EXE'
	.BYTE	'C'+80H
	.WORD	QCOMP-8
QEXEC:	.WORD	DOCOL
	.WORD	STATE
	.WORD	AT
	.WORD	LIT
	.WORD	12H
	.WORD	QERR
	.WORD	SEMIS
;
	.BYTE	86H	; ?PAIRS
	.ASCII	'?PAIR'
	.BYTE	'S'+80H
	.WORD	QEXEC-8
QPAIR:	.WORD	DOCOL
	.WORD	SUBB
	.WORD	LIT
	.WORD	13H
	.WORD	QERR
	.WORD	SEMIS
;
	.BYTE	84H	; ?CSP
	.ASCII	'?CS'
	.BYTE	'P'+80H
	.WORD	QPAIR-9
QCSP:	.WORD	DOCOL
	.WORD	SPAT
	.WORD	CSPP
	.WORD	AT
	.WORD	SUBB
	.WORD	LIT
	.WORD	14H
	.WORD	QERR
	.WORD	SEMIS
;
	.BYTE	88H	; ?LOADING
	.ASCII	'?LOADIN'
	.BYTE	'G'+80H
	.WORD	QCSP-7
QLOAD:	.WORD	DOCOL
	.WORD	BLK
	.WORD	AT
	.WORD	ZEQU
	.WORD	LIT
	.WORD	16H
	.WORD	QERR
	.WORD	SEMIS
;
	.BYTE	87H	; COMPILE
	.ASCII	'COMPIL'
	.BYTE	'E'+80H
	.WORD	QLOAD-0BH
COMP:	.WORD	DOCOL
	.WORD	QCOMP
	.WORD	FROMR
	.WORD	DUP
	.WORD	TWOP
	.WORD	TOR
	.WORD	AT
	.WORD	COMMA
	.WORD	SEMIS
;
	.BYTE	0C1H; [ ( LEFT BRACKET )  
	.BYTE	'['+80H
	.WORD	COMP-0AH
LBRAC:	.WORD	DOCOL
	.WORD	ZERO
	.WORD	STATE
	.WORD	STORE
	.WORD	SEMIS
;
	.BYTE	81H	; ] (RIGHT BRACKET )
	.BYTE	']'+80H
	.WORD	LBRAC-4
RBRAC:	.WORD	DOCOL
	.WORD	LIT
	.WORD	0C0H
	.WORD	STATE
	.WORD	STORE
	.WORD	SEMIS
;
	.BYTE	86H	; SMUDGE
	.ASCII	'SMUDG'
	.BYTE	'E'+80H
	.WORD	RBRAC-4
SMUDG:	.WORD	DOCOL
	.WORD	LATES
	.WORD	LIT
	.WORD	20H
	.WORD	TOGGL
	.WORD	SEMIS
;
	.BYTE	83H	; HEX
	.ASCII	'HE'
	.BYTE	'X'+80H
	.WORD	SMUDG-9
HEX:	.WORD	DOCOL
	.WORD	LIT
	.WORD	10H
	.WORD	BASE
	.WORD	STORE
	.WORD	SEMIS
;
	.BYTE	87H	; DECIMAL
	.ASCII	'DECIMA'
	.BYTE	'L'+80H
	.WORD	HEX-6
DEC:	.WORD	DOCOL
	.WORD	LIT
	.WORD	0AH
	.WORD	BASE
	.WORD	STORE
	.WORD	SEMIS
;
	.BYTE	87H	; (;CODE)
	.ASCII	'(;CODE'
	.BYTE	')'+80H
	.WORD	DEC-0AH
PSCOD:	.WORD	DOCOL
	.WORD	FROMR
	.WORD	LATES
	.WORD	PFA
	.WORD	CFA
	.WORD	STORE
	.WORD	SEMIS
;
	.BYTE	0C5H	; ;CODE
	.ASCII	';COD'
	.BYTE	'E'+80H
	.WORD	PSCOD-0AH
SEMIC:	.WORD	DOCOL
	.WORD	QCSP
	.WORD	COMP
	.WORD	PSCOD
	.WORD	LBRAC
SEMI1:	.WORD	NOOP	; (ASSEMBLER)
	.WORD	SEMIS
;
	.BYTE	87H	; <BUILDS
	.ASCII	'<BUILD'
	.BYTE	'S'+80H
	.WORD	SEMIC-8
BUILD:	.WORD	DOCOL
	.WORD	ZERO
	.WORD	CON
	.WORD	SEMIS
;
	.BYTE	85H	; DOES>
	.ASCII	'DOES'
	.BYTE	'>'+80H
	.WORD	BUILD-0AH
DOES:	.WORD	DOCOL
	.WORD	FROMR
	.WORD	LATES
	.WORD	PFA
	.WORD	STORE
	.WORD	PSCOD
DODOE:	LHLD	RPP
	DCX	H
	MOV	M,B
	DCX	H
	MOV	M,C
	SHLD	RPP
	INX	D
	XCHG
	MOV	C,M
	INX	H
	MOV	B,M
	INX	H
	JMP	HPUSH
;
	.BYTE	85H	; COUNT
	.ASCII	'COUN'
	.BYTE	'T'+80H
	.WORD	DOES-8
COUNT:	.WORD	DOCOL
	.WORD	DUP
	.WORD	ONEP
	.WORD	SWAP
	.WORD	CAT
	.WORD	SEMIS
;
	.BYTE	84H	; TYPE
	.ASCII	'TYP'
	.BYTE	'E'+80H
	.WORD	COUNT-8
TYPE:	.WORD	DOCOL
	.WORD	DDUP
	.WORD	ZBRAN	; IF
	.WORD	TYPE1-.
	.WORD	OVER
	.WORD	PLUS
	.WORD	SWAP
	.WORD	XDO	; DO
TYPE2:	.WORD	IDO
	.WORD	CAT
	.WORD	EMIT
	.WORD	XLOOP	; LOOP
	.WORD	TYPE2-.
	.WORD	BRAN	; ELSE
	.WORD	TYPE3-.
TYPE1:	.WORD	DROP	; ENDIF
TYPE3:	.WORD	SEMIS
;
	.BYTE	89H	; -TRAILING
	.ASCII	'-TRAILIN'
	.BYTE	'G'+80H
	.WORD	TYPE-7
DTRAI:	.WORD	DOCOL
	.WORD	DUP
	.WORD	ZERO
	.WORD	XDO	; DO
DTRA1:	.WORD	OVER
	.WORD	OVER
	.WORD	PLUS
	.WORD	ONE
	.WORD	SUBB
	.WORD	CAT
	.WORD	BL
	.WORD	SUBB
	.WORD	ZBRAN	; IF
	.WORD	DTRA2-.
	.WORD	LEAVE
	.WORD	BRAN	; ELSE
	.WORD	DTRA3-.
DTRA2:	.WORD	ONE
	.WORD	SUBB	; ENDIF
DTRA3:	.WORD	XLOOP	; LOOP
	.WORD	DTRA1-.
	.WORD	SEMIS
;
	.BYTE	84H	; (.")
	.ASCII	'(."'
	.BYTE	')'+80H
	.WORD	DTRAI-0CH
PDOTQ:	.WORD	DOCOL
	.WORD	RR
	.WORD	COUNT
	.WORD	DUP
	.WORD	ONEP
	.WORD	FROMR
	.WORD	PLUS
	.WORD	TOR
	.WORD	TYPE
	.WORD	SEMIS
;
	.BYTE	0C2H	;  ."
	.BYTE	'.'
	.BYTE	'"'+80H
	.WORD	PDOTQ-7
DOTQ:	.WORD	DOCOL
	.WORD	LIT
	.WORD	22H
	.WORD	STATE
	.WORD	AT
	.WORD	ZBRAN	; IF
	.WORD	DOTQ1-.
	.WORD	COMP
	.WORD	PDOTQ
	.WORD	WORD
	.WORD	HERE
	.WORD	CAT
	.WORD	ONEP
	.WORD	ALLOT
	.WORD	BRAN	; ELSE
	.WORD	DOTQ2-.
DOTQ1:	.WORD	WORD
	.WORD	HERE
	.WORD	COUNT
	.WORD	TYPE	; ENDIF
DOTQ2:	.WORD	SEMIS
;
	.BYTE	86H	; EXPECT
	.ASCII	'EXPEC'
	.BYTE	'T'+80H
	.WORD	DOTQ-5
EXPEC:	.WORD	DOCOL
	.WORD	OVER
	.WORD	PLUS
	.WORD	OVER
	.WORD	XDO	; DO
EXPE1:	.WORD	KEY
	.WORD	DUP
	.WORD	LIT
	.WORD	0EH
	.WORD	PORIG
	.WORD	AT
	.WORD	EQUAL
	.WORD	ZBRAN	; IF
	.WORD	EXPE2-.
	.WORD	DROP
	.WORD	DUP
	.WORD	IDO
	.WORD	EQUAL
	.WORD	DUP
	.WORD	FROMR
	.WORD	TWO
	.WORD	SUBB
	.WORD	PLUS
	.WORD	TOR
	.WORD	ZBRAN	; IF
	.WORD	EXPE6-.
	.WORD	LIT
	.WORD	BELL
	.WORD	BRAN	; ELSE
	.WORD	EXPE7-.
EXPE6:	.WORD	LIT
	.WORD	BSOUT	; ENDIF
EXPE7:	.WORD	BRAN	; ELSE
	.WORD	EXPE3-.
EXPE2:	.WORD	DUP
	.WORD	LIT
	.WORD	0DH
	.WORD	EQUAL
	.WORD	ZBRAN	; IF
	.WORD	EXPE4-.
	.WORD	LEAVE
	.WORD	DROP
	.WORD	BL
	.WORD	ZERO
	.WORD	BRAN	; ELSE
	.WORD	EXPE5-.
EXPE4:	.WORD	DUP	; ENDIF
EXPE5:	.WORD	IDO
	.WORD	CSTOR
	.WORD	ZERO
	.WORD	IDO
	.WORD	ONEP
	.WORD	STORE	; ENDIF
EXPE3:	.WORD	EMIT
	.WORD	XLOOP	; LOOP
	.WORD	EXPE1-.
	.WORD	DROP
	.WORD	SEMIS
;
	.BYTE	85H	; QUERY
	.ASCII	'QUER'
	.BYTE	'Y'+80H
	.WORD	EXPEC-9
QUERY:	.WORD	DOCOL
	.WORD	TIB
	.WORD	AT
	.WORD	LIT
	.WORD	50H
	.WORD	EXPEC
	.WORD	ZERO
	.WORD	INN
	.WORD	STORE
	.WORD	SEMIS
;
	.BYTE	0C1H	; 0  (NULL, BINARY ZERO, 
			;                CNTL @ )
	.BYTE	80H
	.WORD	QUERY-8
NULL:	.WORD	DOCOL
	.WORD	BLK
	.WORD	AT
	.WORD	ZBRAN	; IF
	.WORD	NULL1-.
	.WORD	ONE
	.WORD	BLK
	.WORD	PSTOR
	.WORD	ZERO
	.WORD	INN
	.WORD	STORE
	.WORD	BLK
	.WORD	AT
	.WORD	BSCR
	.WORD	ONE
	.WORD	SUBB
	.WORD	ANDD
	.WORD	ZEQU
	.WORD	ZBRAN	; IF
	.WORD	NULL2-.
	.WORD	QEXEC
	.WORD	FROMR
	.WORD	DROP	; ENDIF
NULL2:	.WORD	BRAN	; ELSE
	.WORD	NULL3-.
NULL1:	.WORD	FROMR
	.WORD	DROP	; ENDIF
NULL3:	.WORD	SEMIS
;
	.BYTE	84H	; FILL
	.ASCII	'FIL'
	.BYTE	'L'+80H
	.WORD	NULL-4
FILL:	.WORD	.+2
	MOV	L,C
	MOV	H,B
	POP	D
	POP	B
	XTHL
	XCHG
FILL1:	MOV	A,B	; BEGIN
	ORA	C
	JZ	FILL2	; WHILE
	MOV	A,L
	STAX	D
	INX	D
	DCX	B
	JMP	FILL1	; REPEAT
FILL2:	POP	B
	JMP	NEXT
;
	.BYTE	85H	; ERASE
	.ASCII	'ERAS'
	.BYTE	'E'+80H
	.WORD	FILL-7
ERASEE:	.WORD	DOCOL
	.WORD	ZERO
	.WORD	FILL
	.WORD	SEMIS
;
	.BYTE	86H	; BLANKS
	.ASCII	'BLANK'
	.BYTE	'S'+80H
	.WORD	ERASEE-8
BLANK:	.WORD	DOCOL
	.WORD	BL
	.WORD	FILL
	.WORD	SEMIS
;
	.BYTE	84H	; HOLD
	.ASCII	'HOL'
	.BYTE	'D'+80H
	.WORD	BLANK-9
HOLD:	.WORD	DOCOL
	.WORD	LIT
	.WORD	-1
	.WORD	HLD
	.WORD	PSTOR
	.WORD	HLD
	.WORD	AT
	.WORD	CSTOR
	.WORD	SEMIS
;
	.BYTE	83H	; PAD
	.ASCII	'PA'
	.BYTE	'D'+80H
	.WORD	HOLD-7
PAD:	.WORD	DOCOL
	.WORD	HERE
	.WORD	LIT
	.WORD	44H
	.WORD	PLUS
	.WORD	SEMIS
;
	.BYTE	84H	; WORD
	.ASCII	'WOR'
	.BYTE	'D'+80H
	.WORD	PAD-6
WORD:	.WORD	DOCOL

WORD1:	.WORD	TIB
	.WORD	AT	; ENDIF
WORD2:	.WORD	INN
	.WORD	AT
	.WORD	PLUS
	.WORD	SWAP
	.WORD	ENCL
	.WORD	HERE
	.WORD	LIT
	.WORD	22H
	.WORD	BLANK
	.WORD	INN
	.WORD	PSTOR
	.WORD	OVER
	.WORD	SUBB
	.WORD	TOR
	.WORD	RR
	.WORD	HERE
	.WORD	CSTOR
	.WORD	PLUS
	.WORD	HERE
	.WORD	ONEP
	.WORD	FROMR
	.WORD	CMOVE
	.WORD	SEMIS
;
	.BYTE	88H	; (NUMBER)
	.ASCII	'(NUMBER'
	.BYTE	')'+80H
	.WORD	WORD-7
PNUMB:	.WORD	DOCOL
PNUM1:	.WORD	ONEP	; BEGIN
	.WORD	DUP
	.WORD	TOR
	.WORD	CAT
	.WORD	BASE
	.WORD	AT
	.WORD	DIGIT
	.WORD	ZBRAN	; WHILE
	.WORD	PNUM2-.
	.WORD	SWAP
	.WORD	BASE
	.WORD	AT
	.WORD	USTAR
	.WORD	DROP
	.WORD	ROT
	.WORD	BASE
	.WORD	AT
	.WORD	USTAR
	.WORD	DPLUS
	.WORD	DPL
	.WORD	AT
	.WORD	ONEP
	.WORD	ZBRAN	; IF
	.WORD	PNUM3-.
	.WORD	ONE
	.WORD	DPL
	.WORD	PSTOR	; ENDIF
PNUM3:	.WORD	FROMR
	.WORD	BRAN	; REPEAT
	.WORD	PNUM1-.
PNUM2:	.WORD	FROMR
	.WORD	SEMIS
;
	.BYTE	86H	; NUMBER
	.ASCII	'NUMBE'
	.BYTE	'R'+80H
	.WORD	PNUMB-0BH
NUMB:	.WORD	DOCOL
	.WORD	ZERO
	.WORD	ZERO
	.WORD	ROT
	.WORD	DUP
	.WORD	ONEP
	.WORD	CAT
	.WORD	LIT
	.WORD	2DH
	.WORD	EQUAL
	.WORD	DUP
	.WORD	TOR
	.WORD	PLUS
	.WORD	LIT
	.WORD	-1
NUMB1:	.WORD	DPL	; BEGIN
	.WORD	STORE
	.WORD	PNUMB
	.WORD	DUP
	.WORD	CAT
	.WORD	BL
	.WORD	SUBB
	.WORD	ZBRAN	; WHILE
	.WORD	NUMB2-.
	.WORD	DUP
	.WORD	CAT
	.WORD	LIT
	.WORD	2EH
	.WORD	SUBB
	.WORD	ZERO
	.WORD	QERR
	.WORD	ZERO
	.WORD	BRAN	; REPEAT
	.WORD	NUMB1-.
NUMB2:	.WORD	DROP
	.WORD	FROMR
	.WORD	ZBRAN	; IF
	.WORD	NUMB3-.
	.WORD	DMINU	; ENDIF
NUMB3:	.WORD	SEMIS
;
	.BYTE	85H	; -FIND (0-3) SUCCESS
	.ASCII	'-FIN'	;       (0-1) FAILURE
	.BYTE	'D'+80H
	.WORD	NUMB-9
DFIND:	.WORD	DOCOL
	.WORD	BL
	.WORD	WORD
	.WORD	HERE
	.WORD	CONT
	.WORD	AT
	.WORD	AT
	.WORD	PFIND
	.WORD	DUP
	.WORD	ZEQU
	.WORD	ZBRAN	; IF
	.WORD	DFIN1-.
	.WORD	DROP
	.WORD	HERE
	.WORD	LATES
	.WORD	PFIND	; ENDIF
DFIN1:	.WORD	SEMIS
;
	.BYTE	87H	; (ABORT)
	.ASCII	'(ABORT'
	.BYTE	')'+80H
	.WORD	DFIND-8
PABOR:	.WORD	DOCOL
	.WORD	ABORT
	.WORD	SEMIS
;
	.BYTE	85H	; ERROR
	.ASCII	'ERRO'
	.BYTE	'R'+80H
	.WORD	PABOR-0AH
ERROR:	.WORD	DOCOL
	.WORD	WARN
	.WORD	AT
	.WORD	ZLESS
	.WORD	ZBRAN	; IF
	.WORD	ERRO1-.
	.WORD	PABOR	; ENDIF
ERRO1:	.WORD	HERE
	.WORD	COUNT
	.WORD	TYPE
	.WORD	PDOTQ
	.BYTE	2
	.ASCII	'? '
	.WORD	MESS
	.WORD	SPSTO
;	CHANGE FROM FIG MODEL
;	.WORD	INN,AT,BLK,AT
	.WORD	BLK
	.WORD	AT
	.WORD	DDUP
	.WORD	ZBRAN
	.WORD	ERRO2-.	; IF
	.WORD	INN
	.WORD	AT
	.WORD	SWAP		; ENDIF
ERRO2:	.WORD	QUIT
;
	.BYTE	83H	; ID.
	.ASCII	'ID'
	.BYTE	'.'+80H
	.WORD	ERROR-8
IDDOT:	.WORD	DOCOL
	.WORD	PAD
	.WORD	LIT
	.WORD	20H
	.WORD	LIT
	.WORD	5FH
	.WORD	FILL
	.WORD	DUP
	.WORD	PFA
	.WORD	LFA
	.WORD	OVER
	.WORD	SUBB
	.WORD	PAD
	.WORD	SWAP
	.WORD	CMOVE
	.WORD	PAD
	.WORD	COUNT
	.WORD	LIT
	.WORD	1FH
	.WORD	ANDD
	.WORD	TYPE
	.WORD	SPACE
	.WORD	SEMIS
;
	.BYTE	86H	; CREATE
	.ASCII	'CREAT'
	.BYTE	'E'+80H
	.WORD	IDDOT-6
CREAT:	.WORD	DOCOL
	.WORD	DFIND
	.WORD	ZBRAN	; IF
	.WORD	CREA1-.
	.WORD	DROP
	.WORD	NFA
	.WORD	IDDOT
	.WORD	LIT
	.WORD	4
	.WORD	MESS
	.WORD	SPACE	; ENDIF
CREA1:	.WORD	HERE
	.WORD	DUP
	.WORD	CAT
	.WORD	WIDTH
	.WORD	AT
	.WORD	MIN
	.WORD	ONEP
	.WORD	ALLOT
	.WORD	DUP
	.WORD	LIT
	.WORD	0A0H
	.WORD	TOGGL
	.WORD	HERE
	.WORD	ONE
	.WORD	SUBB
	.WORD	LIT
	.WORD	80H
	.WORD	TOGGL
	.WORD	LATES
	.WORD	COMMA
	.WORD	CURR
	.WORD	AT
	.WORD	STORE
	.WORD	HERE
	.WORD	TWOP
	.WORD	COMMA
	.WORD	SEMIS
;
	.BYTE	0C9H	; [COMPILE]
	.ASCII	'[COMPILE'
	.BYTE	']'+80H
	.WORD	CREAT-9
BCOMP:	.WORD	DOCOL
	.WORD	DFIND
	.WORD	ZEQU
	.WORD	ZERO
	.WORD	QERR
	.WORD	DROP
	.WORD	CFA
	.WORD	COMMA
	.WORD	SEMIS
;
	.BYTE	0C7H	; LITERAL
	.ASCII	'LITERA'
	.BYTE	'L'+80H
	.WORD	BCOMP-0CH
LITER:	.WORD	DOCOL
	.WORD	STATE
	.WORD	AT
	.WORD	ZBRAN	; IF
	.WORD	LITE1-.
	.WORD	COMP
	.WORD	LIT
	.WORD	COMMA	; ENDIF
LITE1:	.WORD	SEMIS
;
	.BYTE	0C8H	; DLITERAL
	.ASCII	'DLITERA'
	.BYTE	'L'+80H
	.WORD	LITER-0AH
DLITE:	.WORD	DOCOL
	.WORD	STATE
	.WORD	AT
	.WORD	ZBRAN	; IF
	.WORD	DLIT1-.
	.WORD	SWAP
	.WORD	LITER
	.WORD	LITER	; ENDIF
DLIT1:	.WORD	SEMIS
;
	.BYTE	86H	; ?STACK
	.ASCII	'?STAC'
	.BYTE	'K'+80H
	.WORD	DLITe-0BH
QSTAC:	.WORD	DOCOL
	.WORD	SPAT
	.WORD	SZERO
	.WORD	AT
	.WORD	SWAP
	.WORD	ULESS
	.WORD	ONE
	.WORD	QERR
	.WORD	SPAT
	.WORD	HERE
	.WORD	LIT
	.WORD	80H
	.WORD	PLUS
	.WORD	ULESS
	.WORD	LIT
	.WORD	7
	.WORD	QERR
	.WORD	SEMIS
;
	.BYTE	89H	; INTERPRET
	.ASCII	'INTERPRE'
	.BYTE	'T'+80H
	.WORD	QSTAC-9
INTER:	.WORD	DOCOL
INTE1:	.WORD	DFIND	; BEGIN
	.WORD	ZBRAN	; IF
	.WORD	INTE2-.
	.WORD	STATE
	.WORD	AT
	.WORD	LESS
	.WORD	ZBRAN	; IF
	.WORD	INTE3-.
	.WORD	CFA
	.WORD	COMMA
	.WORD	BRAN	; ELSE
	.WORD	INTE4-.
INTE3:	.WORD	CFA
	.WORD	EXEC	; ENDIF
INTE4:	.WORD	QSTAC
	.WORD	BRAN	; ELSE
	.WORD	INTE5-.
INTE2:	.WORD	HERE
	.WORD	NUMB
	.WORD	DPL
	.WORD	AT
	.WORD	ONEP
	.WORD	ZBRAN	; IF
	.WORD	INTE6-.
	.WORD	DLITE
	.WORD	BRAN	; ELSE
	.WORD	INTE7-.
INTE6:	.WORD	DROP
	.WORD	LITER	; ENDIF
INTE7:	.WORD	QSTAC	; ENDIF
INTE5:	.WORD	BRAN	; AGAIN
	.WORD	INTE1-.
;
	.BYTE	89H	; IMMEDIATE
	.ASCII	'IMMEDIAT'
	.BYTE	'E'+80H
	.WORD	INTER-0CH
IMMED:	.WORD	DOCOL
	.WORD	LATES
	.WORD	LIT
	.WORD	40H
	.WORD	TOGGL
	.WORD	SEMIS
;
	.BYTE	8AH	; VOCABULARY
	.ASCII	'VOCABULAR'
	.BYTE	'Y'+80H
	.WORD	IMMED-0CH
VOCAB:	.WORD	DOCOL
	.WORD	BUILD
	.WORD	LIT
	.WORD	0A081H
	.WORD	COMMA
	.WORD	CURR
	.WORD	AT
	.WORD	CFA
	.WORD	COMMA
	.WORD	HERE
	.WORD	VOCL
	.WORD	AT
	.WORD	COMMA
	.WORD	VOCL
	.WORD	STORE
	.WORD	DOES
DOVOC:	.WORD	TWOP
	.WORD	CONT
	.WORD	STORE
	.WORD	SEMIS

;	FORTH USED TO BE HERE
;
	.BYTE	8BH	; DEFINITIONS
	.ASCII	'DEFINITION'
	.BYTE	'S'+80H
	.WORD	VOCAB-13
DEFIN:	.WORD	DOCOL
	.WORD	CONT
	.WORD	AT
	.WORD	CURR
	.WORD	STORE
	.WORD	SEMIS
;
	.BYTE	0C1H	; (
	.BYTE	'('+80H
	.WORD	DEFIN-0EH
PAREN:	.WORD	DOCOL
	.WORD	LIT
	.WORD	29H
	.WORD	WORD
	.WORD	SEMIS
;
	.BYTE	84H	; QUIT
	.ASCII	'QUI'
	.BYTE	'T'+80H
	.WORD	PAREN-4
QUIT:	.WORD	DOCOL
	.WORD	ZERO
	.WORD	BLK
	.WORD	STORE
	.WORD	LBRAC
QUIT1:	.WORD	RPSTO	; BEGIN
	.WORD	CR
	.WORD	QUERY
	.WORD	INTER
	.WORD	STATE
	.WORD	AT
	.WORD	ZEQU
	.WORD	ZBRAN	; IF
	.WORD	QUIT2-.
	.WORD	PDOTQ
	.BYTE	2
	.ASCII	'OK'	; ENDIF
QUIT2:	.WORD	BRAN	; AGAIN
	.WORD	QUIT1-.
;
	.BYTE	85H	; ABORT
	.ASCII	'ABOR'
	.BYTE	'T'+80H
	.WORD	QUIT-7
ABORT:	.WORD	DOCOL
	.WORD	SPSTO
	.WORD	DEC
	.WORD	QSTAC
	.WORD	CR
	.WORD	PDOTQ
	.BYTE	0DH+7	;COUNT FOR NEXT 14 CHARACTERS
	.ASCII	'M.C.B. FIG-FORTH '
	.BYTE	FIGREL+30H
	.BYTE	ADOT
	.BYTE	FIGREV+30H
	.WORD	PDOTQ
	.BYTE	27.	;NUMBER OF CHARACTERS IN 
			;    STRING
	.ASCII	'  * 15:55z  14 DECEMBER 83 '
	.WORD	FORTH
	.WORD	DEFIN
	.WORD	QUIT
;
WRM:
	LXI	B,WRM1
	JMP	NEXT
WRM1:	.WORD	WARM
;
	.BYTE	84H	; WARM
	.ASCII	'WAR'
	.BYTE	'M'+80H
	.WORD	ABORT-8
WARM:	.WORD	DOCOL
	.WORD	ABORT
;
CLD:	LXI	B,CLD1
	LHLD	ORIG+12H
	SPHL
INIT:	CALL	CT53IN		;INITIALIZE 8253
	CALL	SIOINT		;INITIALIZE SIO
	LXI	B,CLD1
	JMP	NEXT
CLD1:	.WORD	COLD
;
	.BYTE	84H	; COLD
	.ASCII	'COL'
	.BYTE	'D'+80H
	.WORD	WARM-7
COLD:	.WORD	DOCOL

;
	.WORD	LIT
	.WORD	ORIG+12H
	.WORD	LIT
	.WORD	UP
	.WORD	AT
	.WORD	LIT
	.WORD	6
	.WORD	PLUS
	.WORD	LIT
	.WORD	10H
	.WORD	CMOVE
	.WORD	LIT
	.WORD	ORIG+0CH
	.WORD	AT
	.WORD	LIT
	.WORD	FORTH+6
	.WORD	STORE
	.WORD	ABORT
;
	.BYTE	84H	; S->D
	.ASCII	'S->'
	.BYTE	'D'+80H
	.WORD	COLD-7
STOD:	.WORD	.+2
	POP	D
	LXI	H,0
	MOV	A,D
	ANI	80H
	JZ	STOD1
	DCX	H
STOD1:	JMP	DPUSH
;
	.BYTE	82H	; +-
	.BYTE	'+'
	.BYTE	'-'+80H
	.WORD	STOD-7
PM:	.WORD	DOCOL
	.WORD	ZLESS
	.WORD	ZBRAN	; IF
	.WORD	PM1-.
	.WORD	MINUS	; ENDIF
PM1:	.WORD	SEMIS
;
	.BYTE	83H	; D+-
	.ASCII	'D+'
	.BYTE	'-'+80H
	.WORD	PM-5
DPM:	.WORD	DOCOL
	.WORD	ZLESS
	.WORD	ZBRAN	; IF
	.WORD	DPM1-.
	.WORD	DMINU	; ENDIF
DPM1:	.WORD	SEMIS
;
	.BYTE	83H	; ABS
	.ASCII	'AB'
	.BYTE	'S'+80H
	.WORD	DPM-6
ABS:	.WORD	DOCOL
	.WORD	DUP
	.WORD	PM
	.WORD	SEMIS
;
	.BYTE	84H	; DABS
	.ASCII	'DAB'
	.BYTE	'S'+80H
	.WORD	ABS-6
DABS:	.WORD	DOCOL
	.WORD	DUP
	.WORD	DPM
	.WORD	SEMIS
;
	.BYTE	83H	; MIN
	.ASCII	'MI'
	.BYTE	'N'+80H
	.WORD	DABS-7
MIN:	.WORD	DOCOL
	.WORD	TDUP
	.WORD	GREAT
	.WORD	ZBRAN	; IF
	.WORD	MIN1-.
	.WORD	SWAP	; ENDIF
MIN1:	.WORD	DROP
	.WORD	SEMIS
;
	.BYTE	83H	; MAX
	.ASCII	'MA'
	.BYTE	'X'+80H
	.WORD	MIN-6
MAX:	.WORD	DOCOL
	.WORD	TDUP
	.WORD	LESS
	.WORD	ZBRAN	; IF
	.WORD	MAX1-.
	.WORD	SWAP	; ENDIF
MAX1:	.WORD	DROP
	.WORD	SEMIS
;
	.BYTE	82H	; M*
	.BYTE	'M'
	.BYTE	'*'+80H
	.WORD	MAX-6
MSTAR:	.WORD	DOCOL
	.WORD	TDUP
	.WORD	XORR
	.WORD	TOR
	.WORD	ABS
	.WORD	SWAP
	.WORD	ABS
	.WORD	USTAR
	.WORD	FROMR
	.WORD	DPM
	.WORD	SEMIS
;
	.BYTE	82H	; M/
	.BYTE	'M'
	.BYTE	'/'+80H
	.WORD	MSTAR-5
MSLAS:	.WORD	DOCOL
	.WORD	OVER
	.WORD	TOR
	.WORD	TOR
	.WORD	DABS
	.WORD	RR
	.WORD	ABS
	.WORD	USLAS
	.WORD	FROMR
	.WORD	RR
	.WORD	XORR
	.WORD	PM
	.WORD	SWAP
	.WORD	FROMR
	.WORD	PM
	.WORD	SWAP
	.WORD	SEMIS
;
	.BYTE	81H	; *
	.BYTE	'*'+80H
	.WORD	MSLAS-5
STAR:	.WORD	DOCOL
	.WORD	MSTAR
	.WORD	DROP
	.WORD	SEMIS
;
	.BYTE	84H	; /MOD
	.ASCII	'/MO'
	.BYTE	'D'+80H
	.WORD	STAR-4
SLMOD:	.WORD	DOCOL
	.WORD	TOR
	.WORD	STOD
	.WORD	FROMR
	.WORD	MSLAS
	.WORD	SEMIS
;
	.BYTE	81H	; /
	.BYTE	'/'+80H
	.WORD	SLMOD-7
SLASH:	.WORD	DOCOL
	.WORD	SLMOD
	.WORD	SWAP
	.WORD	DROP
	.WORD	SEMIS
;
	.BYTE	83H	; MOD
	.ASCII	'MO'
	.BYTE	'D'+80H
	.WORD	SLASH-4
MODD:	.WORD	DOCOL
	.WORD	SLMOD
	.WORD	DROP
	.WORD	SEMIS
;
	.BYTE	85H	; */MOD
	.ASCII	'*/MO'
	.BYTE	'D'+80H
	.WORD	MODD-6
SSMOD:	.WORD	DOCOL
	.WORD	TOR
	.WORD	MSTAR
	.WORD	FROMR
	.WORD	MSLAS
	.WORD	SEMIS
;
	.BYTE	82H	; */
	.BYTE	'*'
	.BYTE	'/'+80H
	.WORD	SSMOD-8
SSLA:	.WORD	DOCOL
	.WORD	SSMOD
	.WORD	SWAP
	.WORD	DROP
	.WORD	SEMIS
;
	.BYTE	85H	; M/MOD
	.ASCII	'M/MO'
	.BYTE	'D'+80H
	.WORD	SSLA-5
MSMOD:	.WORD	DOCOL
	.WORD	TOR
	.WORD	ZERO
	.WORD	RR
	.WORD	USLAS
	.WORD	FROMR
	.WORD	SWAP
	.WORD	TOR
	.WORD	USLAS
	.WORD	FROMR
	.WORD	SEMIS

	.BYTE	87H	; MESSAGE
	.ASCII	'MESSAG'
	.BYTE	'E'+80H
	.WORD	MSMOD-8
MESS:	.WORD	DOCOL
MESS1:	.WORD	PDOTQ
	.BYTE	6
	.ASCII	'MSG # '
	.WORD	DOT	; ENDIF
MESS3:	.WORD	SEMIS
;
; END FVOC
; P! FILE
;-------------------------------------
;	8080 PORT FETCH AND STORE
;
;
	.BYTE	82H	; P@   ( PORT @ )
	.BYTE	'P'
	.BYTE	'@'+80H
	.WORD	MESS-0AH
PTAT:	.WORD	.+2
	PUSH	B	; SAVE IR
	POP	D	; IN DE
	POP	B	; PORT # IN C
	INP	L	; INPUT TO L
	MVI	H,0
	PUSH	D
	POP	B	; RESTORE IR
	JMP	HPUSH
;
	.BYTE	82H	; P!  (PORT STORE)
	.BYTE	'P'
	.BYTE	'!'+80H
	.WORD	PTAT-5
PTSTO:	.WORD	.+2
	PUSH	B	; SAVE IR
	POP	D	; IN DE
	POP	B	; E <- PORT#
	POP	H	; L= VALUE
	OUTP	L	; ( PORT# MODIFIED )
	PUSH	D
	POP	B	; RESTORE IR
	JMP	NEXT
;
;---------------------------------
; END P!
; DIO FILE
;---------------------------------

;	ALTERNATIVE R/W FOR NO DISK INTERFACE

	.BYTE	83H	; R/W ( DISK PRIMITIVE )
	.ASCII	'R/'
	.BYTE	'W'+80H
	.WORD	PTSTO-5
RSLW:	.WORD	DOCOL
	.WORD	DROP
	.WORD	DROP
	.WORD	DROP
	.WORD	SEMIS
;
;------------------------------

;
	.BYTE	84H	; LOAD
	.ASCII	'LOA'
	.BYTE	'D'+80H
	.WORD	RSLW-6
LOAD:	.WORD	DOCOL
	.WORD	BLK
	.WORD	AT
	.WORD	TOR
	.WORD	INN
	.WORD	AT
	.WORD	TOR
	.WORD	ZERO
	.WORD	INN
	.WORD	STORE
	.WORD	BSCR
	.WORD	STAR
	.WORD	BLK
	.WORD	STORE	; BLK <- SCR * B/SCR
	.WORD	INTER	; INTERPRET FROM OTHER SCREEN
	.WORD	FROMR
	.WORD	INN
	.WORD	STORE
	.WORD	FROMR
	.WORD	BLK
	.WORD	STORE
	.WORD	SEMIS
;
;---------------------------------------
;
;
;  EPRINT IS A USER VARIABLE : 36h OFFSET
;
CSTAT:
	IN	TTS	; CONSOLE STATUS
	RAR		; CHECK IF ANY CHR 
			;     HAS BEEN TYPED
	MVI	A,0FFh	;IF CHR TYPED THEN (A) <- 0FFH
	RC		
	INR	A	;ELSE (A) <- 0
	RET		; CHR IGNORED
;
CIN:	IN	TTS
	RAR		; BUFFER EMPTY?
	JRNC	CIN
	IN	TTI
	RET

;
COUT:	IN	TTS
	ANI	TTYBE	; BUFFER EMPTY
	JRZ	COUT
	MOV	A,C
	OUT	TTO
	RET
;
POUT:	IN	TTS
	ANI	TTYBE	; BUFFER EMPTY?
	JRZ	POUT
	RET
;
CPOUT:	CALL	COUT	; OUTPUT (C) TO CONSOLE
	XCHG
	LXI	H,EPRINT
	MOV	A,M	; IF (EPRINT) <> 0
	ORA	A
	JZ	CPOU1
	MOV	C,E	; THEN OUTPUT (C) TO PRINTER
	CALL	POUT
CPOU1:	RET
;
;	FORTH SERIAL IO INTERFACE
;
PQTER:	CALL	CSTAT	; IF CHR TYPED
	LXI	H,0
	ORA	A
	JZ	PQTE1
	INR	L	; THEN (S1) <- TRUE
PQTE1:	JMP	HPUSH	; ELSE (S1) <- FALSE
;
PKEY:	CALL	CIN	; READ CHR FROM CONSOLE
	CPI	DLE	;IF CHR = (^P)...Data 
			;           Link Escape
	MOV	E,A
	JNZ	PKEY1
	LXI	H,EPRINT	; THEN TOGGLE 
				;   (EPRINT) LSB
	MVI	E,ABL	; CHR <- BLANK
	MOV	A,M
	XRI	1
	MOV	M,A
PKEY1:	MOV	L,E
	MVI	H,0
	JMP	HPUSH	; (S1)LB <- CHR
;
PEMIT:	.WORD	.+2	; (EMIT)   ORPHAN
	POP	H	; (L) <- (S1)LB = CHR
	PUSH	B	; save (IP)
	MOV	C,L
	CALL	CPOUT	; OUTPUT CHAR TO CONSOLE
			; AND MAYBE PRINTER
	POP	B	; RESTORE (IP)
	JMP	NEXT
;
PCR:	PUSH	B	; SAVE (IP)
	MVI	C,ACR	; OUTPUT (CR) TO CONSOLE
	MOV	L,C
	CALL	CPOUT	; and mayve to printer
	MVI	C,LF	; OUTPUT (LF) TO CONSOLE
	MOV	L,C
	CALL	CPOUT	; AND maybe to printer
	POP	B	; RESTORE (IP)
	JMP	NEXT
; END CON
; D4 FILE
;----------------------------------
;
	.BYTE	0C1H	; '  ( tick )
	.BYTE	0A7H
	.WORD	LOAD-7
TICK:	.WORD	DOCOL
	.WORD	DFIND
	.WORD	ZEQU
	.WORD	ZERO
	.WORD	QERR
	.WORD	DROP
	.WORD	LITER
	.WORD	SEMIS
;
	.BYTE	86H	; FORGET
	.ASCII	'FORGE'
	.BYTE	'T'+80H
	.WORD	TICK-4
FORG:	.WORD	DOCOL
	.WORD	CURR
	.WORD	AT
	.WORD	CONT
	.WORD	AT
	.WORD	SUBB
	.WORD	LIT
	.WORD	18H
	.WORD	QERR
	.WORD	TICK
	.WORD	DUP
	.WORD	FENCE
	.WORD	AT
	.WORD	LESS
	.WORD	LIT
	.WORD	15H
	.WORD	QERR
	.WORD	DUP
	.WORD	NFA
	.WORD	DP
	.WORD	STORE
	.WORD	LFA
	.WORD	AT
	.WORD	CONT
	.WORD	AT
	.WORD	STORE
	.WORD	SEMIS
;
	.BYTE	84H	; BACK
	.ASCII	'BAC'
	.BYTE	'K'+80H
	.WORD	FORG-9
BACK:	.WORD	DOCOL
	.WORD	HERE
	.WORD	SUBB
	.WORD	COMMA
	.WORD	SEMIS
;
	.BYTE	0C5H	; BEGIN
	.ASCII	'BEGI'
	.BYTE	'N'+80H
	.WORD	BACK-7
BEGIN:	.WORD	DOCOL
	.WORD	QCOMP
	.WORD	HERE
	.WORD	ONE
	.WORD	SEMIS
;
	.BYTE	0C5H	; ENDIF
	.ASCII	'ENDI'
	.BYTE	'F'+80H
	.WORD	BEGIN-8
ENDIFF:	.WORD	DOCOL
	.WORD	QCOMP
	.WORD	TWO
	.WORD	QPAIR
	.WORD	HERE
	.WORD	OVER
	.WORD	SUBB
	.WORD	SWAP
	.WORD	STORE
	.WORD	SEMIS
;
	.BYTE	0C4H	; THEN
	.ASCII	'THE'
	.BYTE	'N'+80H
	.WORD	ENDIFF-8
THEN:	.WORD	DOCOL
	.WORD	ENDIFF
	.WORD	SEMIS
;
	.BYTE	0C2H	; DO
	.BYTE	'D'
	.BYTE	'O'+80H
	.WORD	THEN-7
DO:	.WORD	DOCOL
	.WORD	COMP
	.WORD	XDO
	.WORD	HERE
	.WORD	THREE
	.WORD	SEMIS
;
	.BYTE	0C4H	; LOOP
	.ASCII	'LOO'
	.BYTE	'P'+80H
	.WORD	DO-5
LOOP:	.WORD	DOCOL
	.WORD	THREE
	.WORD	QPAIR
	.WORD	COMP
	.WORD	XLOOP
	.WORD	BACK
	.WORD	SEMIS
;
	.BYTE	0C5H	; +LOOP
	.ASCII	'+LOO'
	.BYTE	'P'+80H
	.WORD	LOOP-7
PLOOP:	.WORD	DOCOL
	.WORD	THREE
	.WORD	QPAIR
	.WORD	COMP
	.WORD	XPLOO
	.WORD	BACK
	.WORD	SEMIS
;
	.BYTE	0C5H	; UNTIL
	.ASCII	'UNTI'
	.BYTE	'L'+80H
	.WORD	PLOOP-8
UNTIL:	.WORD	DOCOL
	.WORD	ONE
	.WORD	QPAIR
	.WORD	COMP
	.WORD	ZBRAN
	.WORD	BACK
	.WORD	SEMIS
;
	.BYTE	0C3H	; END
	.ASCII	'EN'
	.BYTE	'D'+80H
	.WORD	UNTIL-8
ENDD:	.WORD	DOCOL
	.WORD	UNTIL
	.WORD	SEMIS
;
	.BYTE	0C5H	; AGAIN
	.ASCII	'AGAI'
	.BYTE	'N'+80H
	.WORD	ENDD-6
AGAIN:	.WORD	DOCOL
	.WORD	ONE
	.WORD	QPAIR
	.WORD	COMP
	.WORD	BRAN
	.WORD	BACK
	.WORD	SEMIS
;
	.BYTE	0C6H	; REPEAT
	.ASCII	'REPEA'
	.BYTE	'T'+80H
	.WORD	AGAIN-8
REPEA:	.WORD	DOCOL
	.WORD	TOR
	.WORD	TOR
	.WORD	AGAIN
	.WORD	FROMR
	.WORD	FROMR
	.WORD	TWO
	.WORD	SUBB
	.WORD	ENDIFF
	.WORD	SEMIS
;
	.BYTE	0C2H	; IF
	.BYTE	'I'
	.BYTE	'F'+80H
	.WORD	REPEA-9
IFF:	.WORD	DOCOL
	.WORD	COMP
	.WORD	ZBRAN
	.WORD	HERE
	.WORD	ZERO
	.WORD	COMMA
	.WORD	TWO
	.WORD	SEMIS
;
	.BYTE	0C4H	; ELSE
	.ASCII	'ELS'
	.BYTE	'E'+80H
	.WORD	IFF-5
ELSEE:	.WORD	DOCOL
	.WORD	TWO
	.WORD	QPAIR
	.WORD	COMP
	.WORD	BRAN
	.WORD	HERE
	.WORD	ZERO
	.WORD	COMMA
	.WORD	SWAP
	.WORD	TWO
	.WORD	ENDIFF
	.WORD	TWO
	.WORD	SEMIS
;
	.BYTE	0C5H	; WHILE
	.ASCII	'WHIL'
	.BYTE	'E'+80H
	.WORD	ELSEE-7
WHILE:	.WORD	DOCOL
	.WORD	IFF
	.WORD	TWOP
	.WORD	SEMIS
;
	.BYTE	86H	; SPACES
	.ASCII	'SPACE'
	.BYTE	'S'+80H
	.WORD	 WHILE-8
SPACS:	.WORD	DOCOL
	.WORD	ZERO
	.WORD	MAX
	.WORD	DDUP
	.WORD	ZBRAN	; IF
	.WORD	SPAX1-.
	.WORD	ZERO
	.WORD	XDO	; DO
SPAX2:	.WORD	SPACE
	.WORD	XLOOP	; LOOP ENDIF
	.WORD	SPAX2-.
SPAX1:	.WORD	SEMIS
;
	.BYTE	82H	; <#
	.BYTE	'<'
	.BYTE	'#'+80H
	.WORD	SPACS-9
BDIGS:	.WORD	DOCOL
	.WORD	PAD
	.WORD	HLD
	.WORD	STORE
	.WORD	SEMIS
;
	.BYTE	82H	; #>
	.BYTE	'#'
	.BYTE	'>'+80H
	.WORD	BDIGS-5
EDIGS:	.WORD	DOCOL
	.WORD	DROP
	.WORD	DROP
	.WORD	HLD
	.WORD	AT
	.WORD	PAD
	.WORD	OVER
	.WORD	SUBB
	.WORD	SEMIS
;
	.BYTE	84H	; SIGN
	.ASCII	'SIG'
	.BYTE	'N'+80H
	.WORD	EDIGS-5
SIGN:	.WORD	DOCOL
	.WORD	ROT
	.WORD	ZLESS
	.WORD	ZBRAN	; IF
	.WORD	SIGN1-.
	.WORD	LIT
	.WORD	2DH
	.WORD	HOLD	; ENDIF
SIGN1:	.WORD	SEMIS
;
	.BYTE	81H	; #
	.BYTE	'#'+80H
	.WORD	SIGN-7
DIG:	.WORD	DOCOL
	.WORD	BASE
	.WORD	AT
	.WORD	MSMOD
	.WORD	ROT
	.WORD	LIT
	.WORD	9
	.WORD	OVER
	.WORD	LESS
	.WORD	ZBRAN	; IF
	.WORD	DIG1-.
	.WORD	LIT
	.WORD	7
	.WORD	PLUS	; ENDIF
DIG1:	.WORD	LIT
	.WORD	30H
	.WORD	PLUS
	.WORD	HOLD
	.WORD	SEMIS
;
	.BYTE	82H	; #S
	.BYTE	'#'
	.BYTE	'S'+80H
	.WORD	DIG-4
DIGS:	.WORD	DOCOL
DIGS1:	.WORD	DIG	; BEGIN
	.WORD	OVER
	.WORD	OVER
	.WORD	ORR
	.WORD	ZEQU
	.WORD	ZBRAN	; UNTIL
	.WORD	DIGS1-.
	.WORD	SEMIS
;
	.BYTE	83H	; D.R
	.ASCII	'D.'
	.BYTE	'R'+80H
	.WORD	DIGS-5
DDOTR:	.WORD	DOCOL
	.WORD	TOR
	.WORD	SWAP
	.WORD	OVER
	.WORD	DABS
	.WORD	BDIGS
	.WORD	DIGS
	.WORD	SIGN
	.WORD	EDIGS
	.WORD	FROMR
	.WORD	OVER
	.WORD	SUBB
	.WORD	SPACS
	.WORD	TYPE
	.WORD	SEMIS
;
	.BYTE	82H	; .R
	.BYTE	'.'
	.BYTE	'R'+80H
	.WORD	DDOTR-6
DOTR:	.WORD	DOCOL
	.WORD	TOR
	.WORD	STOD
	.WORD	FROMR
	.WORD	DDOTR
	.WORD	SEMIS
;
	.BYTE	82H	; D.
	.BYTE	'D'
	.BYTE	'.'+80H
	.WORD	DOTR-5
DDOT:	.WORD	DOCOL
	.WORD	ZERO
	.WORD	DDOTR
	.WORD	SPACE
	.WORD	SEMIS
;
	.BYTE	81H	; .
	.BYTE	'.'+80H
	.WORD	DDOT-5
DOT:	.WORD	DOCOL
	.WORD	STOD
	.WORD	DDOT
	.WORD	SEMIS
;
	.BYTE	81H	; ?
	.BYTE	'?'+80H
	.WORD	DOT-4
QUES:	.WORD	DOCOL
	.WORD	AT
	.WORD	DOT
	.WORD	SEMIS
;
	.BYTE	82H	; U.
	.BYTE	'U'
	.BYTE	'.'+80H
	.WORD	QUES-4
UDOT:	.WORD	DOCOL
	.WORD	ZERO
	.WORD	DDOT
	.WORD	SEMIS
;
	.BYTE	85H	; VLIST
	.ASCII	'VLIS'
	.BYTE	'T'+80H
	.WORD	UDOT-5
VLIST:	.WORD	DOCOL
	.WORD	LIT
	.WORD	80H
	.WORD	OUTT
	.WORD	STORE
	.WORD	CONT
	.WORD	AT
	.WORD	AT
VLIS1:	.WORD	OUTT	; BEGIN
	.WORD	AT
	.WORD	CSLL
	.WORD	GREAT
	.WORD	ZBRAN	; IF
	.WORD	VLIS2-.
	.WORD	CR
	.WORD	ZERO
	.WORD	OUTT
	.WORD	STORE	; ENDIF2
VLIS2:	.WORD	DUP
	.WORD	IDDOT
	.WORD	SPACE
	.WORD	SPACE
	.WORD	PFA
	.WORD	LFA
	.WORD	AT
	.WORD	DUP
	.WORD	ZEQU
	.WORD	QTERM
	.WORD	ORR
	.WORD	ZBRAN	; UNTIL
	.WORD	VLIS1-.
	.WORD	DROP
	.WORD	SEMIS
; ENF D4
; BYE FILE
;------EXIT CP/M------------------
;
	.BYTE	83H	; BYE
	.ASCII	'BY'
	.BYTE	'E'+80H
	.WORD	VLIST-8
BYE:	.WORD	.+2
	JMP	0
;---------------------------------
;
	.BYTE	84H	; .CPU
	.ASCII	'.CP'
	.BYTE	'U'+80H
	.WORD	BYE-6
DOTCPU:	.WORD	DOCOL
	.WORD	PDOTQ
	.BYTE	5
	.ASCII	'Z-80 '
	.WORD	SEMIS
;
	.BYTE	84H	; TASK
	.ASCII	'TAS'
	.BYTE	'K'+80H
	.WORD	FORTH-8
TASK:	.WORD	DOCOL
	.WORD	SEMIS

.LOC	RAM-14
R3:
	.BYTE	0C5H	; FORTH
	.ASCII	'FORT'
	.BYTE	'H'+80H
	.WORD	DOTCPU-7
FORTH:	.WORD	DODOE	; DO DOES
	.WORD	DOVOC
	.WORD	0A081H
.LOC	RAM
R4:
	.WORD	FORTH-8	; CHANGED AS VOCABULARY 
			;                 EXTENDED
	.WORD	0

;
INITDP: .BLKB	EM-.	; CONSUME MEMORY TO LIMIT
EM3:
;
.LOC INITR0+32h
   
;----------------------------------------------------
;	FORTH REGISTERS
;
UP:	.WORD	INITR0	;USER AREA POINTER
RPP:	.WORD	INITR0	;RETURN STACK POINTER
;
;----------------------------------------------------
;

EPRINT:	.WORD	0	; ENABLE PRINTER VARIABLE
;			; 0=DISABLED,1=ENABLED


.END	START-100H
S5:


